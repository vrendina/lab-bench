buildscript {
    ext {
        kotlin_version = '1.3.72'
    }
    ext.release = [
            'versionMajor': 0,
            'versionMinor': 1,
            'versionPatch': 0
    ]

    ext.buildConfig = [
            'compileSdk': 29,
            'minSdk'    : 23,
            'targetSdk' : 29
    ]

    ext.versions = [
            'kotlin'    : '1.3.72',
            'coroutines': '1.3.7',
            'dagger'    : '2.27'
    ]
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath 'com.diffplug.spotless:spotless-plugin-gradle:3.27.0'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        java {
            target '**/*.java'
            googleJavaFormat().aosp()
            removeUnusedImports()
            trimTrailingWhitespace()
            indentWithSpaces()
            endWithNewline()
        }
        kotlin {
            target '**/*.kt'
            ktlint('0.28.0')
            trimTrailingWhitespace()
            indentWithSpaces()
            endWithNewline()
        }
        format 'misc', {
            target '**/*.gradle', '**/*.md', '**/.gitignore'
            indentWithSpaces()
            trimTrailingWhitespace()
            endWithNewline()
        }

        format 'xml', {
            target '**/*.xml'
            indentWithSpaces()
            trimTrailingWhitespace()
            endWithNewline()
        }
    }
}


def getGitHash() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}